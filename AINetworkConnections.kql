Resources
| where type contains "searchservice" or type == "microsoft.documentdb/databaseaccounts" or type == "microsoft.cognitiveservices/accounts" and kind == "AIServices" or kind == "OpenAI"
| mv-expand privateEndpointConnection = properties.privateEndpointConnections
| project name, type, kind, location, resourceGroup, subscriptionId,
    PrivateEndpoint = tostring(split(privateEndpointConnection.properties.privateEndpoint.id, "/")[-1])
| join kind=leftouter (
    Resources
    | where type == "microsoft.network/privateendpoints"
    | extend PrivateEndpoint = tostring(split(id, "/")[-1])
    | extend vnetSubnetId = tostring(properties.subnet.id)
    | extend vnet = extract(@"/virtualNetworks/([^/]+)/subnets/([^/]+)", 1, vnetSubnetId)
    | extend subnet = extract(@"/virtualNetworks/([^/]+)/subnets/([^/]+)", 2, vnetSubnetId)
    | project PrivateEndpoint, vnet, subnet, vnetSubnetId
) on PrivateEndpoint
| join kind=leftouter (
    Resources
    | where type contains "microsoft.network/virtualnetworks"
    | mv-expand subnets = properties.subnets
    | extend subnet = tostring(subnets.name)
    | extend NSG = tostring(split(subnets.properties.networkSecurityGroup.id, "/")[-1])
    | project subnet, NSG
) on subnet
| distinct name, type, kind, location, resourceGroup, subscriptionId, PrivateEndpoint, vnet, subnet, NSG
